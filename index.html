<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <link rel="stylesheet" href="_assets/style.css">
    <link rel="icon" href="_assets/icon.svg" type="image/svg+xml" sizes="any">
    <link rel="icon" href="_assets/icon.png" type="image/png" sizes="48x48">
    <script src="_assets/script.js"></script>
    <script id="vert" type="x-shader/x-vertex">#version 300 es
        in vec2 a_pos;

        uniform vec2 u_inputScale;
        uniform vec2 u_inputStep;
        uniform vec2 u_outSize;

        out vec2 v_inputCoord;
        out vec2 v_outCoord;

        void main() {
            gl_Position = vec4(a_pos, 0, 1);
            v_inputCoord = vec2(0.5, -0.5) * a_pos * u_inputScale + vec2(0.5, 0.5);
            v_outCoord = (0.5 * a_pos + vec2(0.5, 0.5));
        }
    </script>
    <script id="frag" type="x-shader/x-fragment">#version 300 es
        #ifdef GL_FRAGMENT_PRECISION_HIGH
            precision highp float;
        #else
            precision mediump float;
        #endif

        uniform vec2 u_inputStep;
        uniform vec2 u_dotsStep;
        uniform vec2 u_outSize;
        
        uniform sampler2D u_input;
        uniform sampler2D u_dots;
        uniform vec2 u_random;

        in vec2 v_inputCoord;
        in vec2 v_outCoord;

        out vec4 color;

        ivec3 permute(ivec3 x) {
            return ((x*34)+10)*x % ivec3(289);
        }
          
        float snoise(vec2 v) {
            const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                                0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                                -0.577350269189626,  // -1.0 + 2.0 * C.x
                                0.024390243902439); // 1.0 / 41.0
            ivec2 i  = ivec2(v + dot(v, C.yy) );
            vec2 x0 = v - vec2(i) + dot(vec2(i), C.xx);
            ivec2 i1 = (x0.x > x0.y) ? ivec2(1, 0) : ivec2(0, 1);
            vec4 x12 = x0.xyxy + C.xxzz + vec4(-vec2(i1), vec2(0.0));
            
            ivec3 p = permute(permute(i.y + ivec3(0, i1.y, 1)) + i.x + ivec3(0, i1.x, 1));
            
            vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
            m = m*m;
            m = m*m;
            
            vec3 x = 2.0 * fract(vec3(p) * C.www) - 1.0;
            vec3 h = abs(x) - 0.5;
            vec3 ox = floor(x + 0.5);
            vec3 a0 = x - ox;
            
            m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
            
            vec3 g = vec3(a0.x  * x0.x  + h.x  * x0.y,  a0.yz * x12.xz + h.yz * x12.yw);
            return 130.0 * dot(m, g);
        }

        float vignette(vec2 uv) {
            vec2 s = u_outSize/max(u_outSize.x, u_outSize.y);
            return 1.0 - smoothstep(0.4, 0.75, distance(s*uv, 0.5*s));
        }

        vec3 colorShift(vec2 uv) {
            float x = smoothstep(0.3, 0.7, distance(v_outCoord*u_outSize/u_outSize.y, vec2(0.5)*u_outSize/u_outSize.y));
            return vec3(4.0*x, 0.0, -4.0*x);
        }

        float luma(vec3 color) {
            return dot(color, vec3(0.299, 0.587, 0.114));
        }          

        void main() {
            vec3 shift = colorShift(v_outCoord);
            vec3 inColor = vec3(
                texture(u_input, v_inputCoord + vec2((shift.r - 1.0)*u_inputStep.x, 0)).r
                + texture(u_input, v_inputCoord + (shift.r + 0.0)*vec2(u_inputStep.x, 0)).r
                + texture(u_input, v_inputCoord + (shift.r + 1.0)*vec2(u_inputStep.x, 0)).r,
                texture(u_input, v_inputCoord + (shift.g - 1.0)*vec2(u_inputStep.x, 0)).g
                + texture(u_input, v_inputCoord + (shift.g + 0.0)*vec2(u_inputStep.x, 0)).g
                + texture(u_input, v_inputCoord + (shift.g + 1.0)*vec2(u_inputStep.x, 0)).g,
                texture(u_input, v_inputCoord + (shift.b - 1.0)*vec2(u_inputStep.x, 0)).b
                + texture(u_input, v_inputCoord + (shift.b + 0.0)*vec2(u_inputStep.x, 0)).b
                + texture(u_input, v_inputCoord + (shift.b + 1.0)*vec2(u_inputStep.x, 0)).b
            ) / 3.0;
        
            vec3 dotsColor = texelFetch(u_dots, ivec2(v_outCoord*u_outSize) % ivec2(6,6), 0).rgb;
            
            vec3 noise = vec3(snoise(u_outSize * v_outCoord / 7.0 + 10.0 * u_random));
            
            vec3 final = mix(noise, inColor, 0.8 + 0.2 * luma(inColor)) * dotsColor * vignette(v_outCoord);
            color = vec4(final, 1.0);
        }
    </script>
    <title>znkr.io</title>
</head>

<body>
    <canvas id="screen"></canvas>
</body>

</html>